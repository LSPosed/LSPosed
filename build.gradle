import org.eclipse.jgit.internal.storage.file.FileRepository
import org.eclipse.jgit.api.Git

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://storage.googleapis.com/r8-releases/raw'
        }
    }
    dependencies {
        classpath 'com.android.tools:r8:3.0.25-dev'
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    }
}

def repo = new FileRepository(rootProject.file(".git"))
def refId = repo.refDatabase.exactRef("refs/remotes/origin/master").objectId
def commitCount = new Git(repo).log().add(refId).call().size()

allprojects {
    ext {
        versionCode = commitCount + 4200
        versionName = versionName
        zipPathMagiskReleasePath = project(":core").projectDir.path + "/build/tmp/release/magisk/"
    }
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/rikkaw/Libraries' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
