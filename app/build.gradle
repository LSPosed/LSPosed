apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    buildFeatures {
        viewBinding = true
    }
    signingConfigs {
        def keystorePwd = null
        def alias = null
        def pwd = null
        if (project.rootProject.file('local.properties').exists()) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            keystorePwd = properties.getProperty("RELEASE_STORE_PASSWORD")
            alias = properties.getProperty("RELEASE_KEY_ALIAS")
            pwd = properties.getProperty("RELEASE_KEY_PASSWORD")
        }
        release {
            storeFile file("edxpmanager.jks")
            storePassword keystorePwd != null ? keystorePwd : System.getenv("KEYSTORE_PASS")
            keyAlias alias != null ? alias : System.getenv("ALIAS_NAME")
            keyPassword pwd != null ? pwd : System.getenv("ALIAS_PASS")
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "org.meowcat.edxposed.manager"
        minSdkVersion 26
        //noinspection OldTargetApi
        targetSdkVersion 27
        versionCode 457000
        versionName "4.5.7.1"
        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "EdXposedManagerR-${defaultConfig.versionName}-${defaultConfig.versionCode}-${buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha03'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha01"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-beta01'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation "com.github.topjohnwu.libsu:core:2.5.1"
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'com.takisoft.preferencex:preferencex-colorpicker:1.1.0'
    implementation 'tech.rectifier.preferencex-android:preferencex-simplemenu:88f93154b2'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.1.0'
    compileOnly 'de.robv.android.xposed:api:82'
}
